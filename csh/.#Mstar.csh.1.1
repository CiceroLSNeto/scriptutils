#!/bin/tcsh
#=======================================================================
#+
# NAME:
#   Mstar.csh
#
# PURPOSE:
#   Estimate stellar mass from input absolute magnitude and/or colour[s]
#
# COMMENTS:
#
# INPUTS:
#   MB                B-band absolute magnitude (Vega) 
#  
# OPTIONAL INPUTS:
#  -B-V               B-V colour (Vega, corrected)
#  -B-R               B-R colour (Vega, corrected)
#  -V-I               V-I colour (Vega, corrected)
#  -V-J               V-J colour (Vega, corrected)
#  -V-H               V-H colour (Vega, corrected)
#  -V-K               V-K colour (Vega, corrected)
#
# OUTPUTS:
#   stdout            log10 Mstar/Msun
#
# EXAMPLES:
#
# BUGS:
#
# REVISION HISTORY:
#   2007-08-02  started Marshall (UCSB)
#-
#=======================================================================

# Options and arguments:

set help = 0
set vb = 0
set MB = 0
set colour = 0
set colourindex = 0
set BV = 0

while ( $#argv > 0 )
   switch ($argv[1])
   case -h:
      set help = 1
      shift argv
      breaksw
   case --help:
      set help = 1
      shift argv
      breaksw
   case -v:        
      set vb = 1
      shift argv
      breaksw
   case --{verbose}:
      set vb = 1
      shift argv
      breaksw
   case -{B-V}:
      shift argv
      set colourindex = 1
      set colour = $argv[1]
      shift argv
      breaksw
   case -{B-R}:
      shift argv
      set colourindex = 2
      set colour = $argv[1]
      shift argv
      breaksw
   case -{V-I}:
      shift argv
      set colourindex = 3
      set colour = $argv[1]
      shift argv
      breaksw
   case -{V-J}:
      shift argv
      set colourindex = 4
      set colour = $argv[1]
      shift argv
      breaksw
   case -{V-H}:
      shift argv
      set colourindex = 5
      set colour = $argv[1]
      shift argv
      breaksw
   case -{V-K}:
      shift argv
      set colourindex = 6
      set colour = $argv[1]
      shift argv
      breaksw
   case *:
      if ($MB == 0) set MB = $argv[1]
      shift argv
      breaksw
   endsw
end

#-----------------------------------------------------------------------

# Catch stupidities, set up variables:

if ($help) then
  print_script_header.csh $0
  goto FINISH
endif

if ($MB == 0) then
  echo "${0:t}: no absolute magnitude provided, aborting"
  goto FINISH
endif  

if ($vb) echo "${0:t}: Estimating stellar mass using recipe from"
if ($vb) echo "${0:t}:   Bell & De Jong (2001), Table 1"
if ($vb) echo "${0:t}: given the B-band absolute Vega magnitude M_B = $MB"

if ($colourindex == 0) then
  if ($vb) echo "${0:t}: WARNING: no colour information provided, winging it"
endif  

#-----------------------------------------------------------------------
# Get M/L ratio:

# Data from Table 1 from Bell & De Jong (2001): 

set colourname = (\
'B-V'\
'B-R'\
'V-I'\
'V-J'\
'V-H'\
'V-K'\
)

set aB = (\
'-0.994'\
'-1.224'\
'-1.919'\
'-1.903'\
'-2.181'\
'-2.156'\
)

set bB = (\
'1.804'\
'1.251'\
'2.214'\
'1.138'\
'0.978'\
'0.895'\
)

# Now compute log10MLratio

if ($colourindex == 0) then
# Guess M/L ratio!
  set MLratio = 5
  if ($vb) echo "${0:t}: WARNING: M/L ratio guessed to be $MLratio"
  set log10MLratio = `echo "l($MLratio)/l(10.0)" | bc -l`

else 
# Compute log M/L ratio from recipe:
  if ($vb) echo "${0:t}: and the $colourname[$colourindex] colour provided ($colour)"
  set log10MLratio = \
    `echo "$aB[$colourindex] + $bB[$colourindex] * $colour" | bc -l`
  
endif

  set i = `echo $log10MLratio | cut -c1` ; set ii = `echo $log10MLratio | cut -c1-2`
  if ($i == '.') set log10MLratio = "0$log10MLratio"  
  if ($ii == '-.') set log10MLratio = "-0"`echo $log10MLratio | cut -c2-`  

set MLratio = `echo "e($log10MLratio * l(10.0))" | bc -l | cut -c1-5`

if ($vb) echo "${0:t}: log10 M/L = $log10MLratio"
if ($vb) echo "${0:t}:       M/L = $MLratio"


#-----------------------------------------------------------------------
# Convert M/L ratio in to stellar mass:

# Solar luminosity in B band from absolutre magnitude tabulated here:
#   http://www.ucolick.org/~cnaw/sun.html

set MBsun = 5.45

set log10LB = `echo "-0.4 * ( $MB - $MBsun )" | bc -l`

  set i = `echo $log10LB | cut -c1` ; set ii = `echo $log10LB | cut -c1-2`
  if ($i == '.') set log10LB = "0$log10LB"  
  if ($ii == '-.') set log10LB = "-0"`echo $log10LB | cut -c2-`  

if ($vb) echo "${0:t}: log10 LB/Lsun = $log10LB"

# And now add to MLratio to get M*:

set log10Mstar = `echo "$log10MLratio + $log10LB" | bc -l`

if ($vb) then
  echo "${0:t}: log10 M*/Msun = $log10Mstar"
else
  echo "$log10Mstar"
endif

#-----------------------------------------------------------------------

# Clean up:

FINISH:

#=======================================================================
